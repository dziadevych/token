pragma solidity ^0.8.6;


import "../openzeppelin-contracts-upgradeable-master/contracts/token/ERC20/IERC20Upgradeable.sol";
import "../openzeppelin-contracts-upgradeable-master/contracts/token/ERC20/utils/SafeERC20Upgradeable.sol";
import "../openzeppelin-contracts-upgradeable-master/contracts/access/OwnableUpgradeable.sol";
import "../openzeppelin-contracts-upgradeable-master/contracts/proxy/utils/Initializable.sol";
import "../openzeppelin-contracts-upgradeable-master/contracts/security/PausableUpgradeable.sol";
import "./interfaces/IPureFiFarming.sol";


 abstract contract PureFiPaymentPlan is Initializable, OwnableUpgradeable, PausableUpgradeable {

  using SafeERC20Upgradeable for IERC20Upgradeable;

  struct Vesting{
    uint8 paymentPlan; //payment plan ID
    uint64 startDate; //payment plan initiation date. Can be 0 if PaymentPlan refers to exact unlock timestamps.
    uint256 totalAmount; //total amount of tokens vested for a person
    uint256 withdrawnAmount; //amount withdrawn by user
  }

  mapping (address => Vesting) internal vestedTokens;

  IERC20Upgradeable public token;
  uint256 public totalVestedAmount; // total amount of vested tokens under this contract control.
  address public farmingContract;
  uint8 public farmingContractPool;

  event Withdrawal(address indexed who, uint256 amount);
  event PaymentPlanAdded(uint256 index);
  event TokensVested(address indexed beneficiary, uint8 paymentPlan, uint64 startDate, uint256 amount);
  
  function initialize(
        address _token
    ) public initializer {
        __Ownable_init();
        __Pausable_init_unchained();

       require(_token != address(0),"incorrect token address");
       token = IERC20Upgradeable(_token);
    }

  function pause() onlyOwner public {
      super._pause();
  }
   
  function unpause() onlyOwner public {
      super._unpause();
  }

  function setFarmingContract(address _farmingContract, uint8 _farmingContractPool) onlyOwner public {
    farmingContract = _farmingContract;
    farmingContractPool = _farmingContractPool;
  }

  function vestTokens(uint8 _paymentPlan, uint64 _startDate, uint256 _amount, address _beneficiary) public onlyOwner whenNotPaused{
    require(vestedTokens[_beneficiary].totalAmount == 0, "This address already has vested tokens");
    require(_isPaymentPlanExists(_paymentPlan), "Incorrect payment plan index");
    require(_amount > 0, "Can't vest 0 tokens");
    require(token.balanceOf(address(this)) >= totalVestedAmount + _amount, "Not enough tokens for this vesting plan");
    vestedTokens[_beneficiary] = Vesting(_paymentPlan, _startDate, _amount, 0);
    totalVestedAmount += _amount;
    emit TokensVested(_beneficiary, _paymentPlan, _startDate, _amount);
  }

  function withdraw(uint256 _amount) public whenNotPaused {
    _prepareWithdraw(_amount);
    token.safeTransfer(msg.sender, _amount);
  }

  function withdrawAndStake(uint256 _amount) public whenNotPaused{
    require(farmingContract != address(0),"Farming contract is not defined");
    _prepareWithdraw(_amount);
    //stake on farming contract instead of withdrawal
    token.safeApprove(farmingContract, _amount);
    IPureFiFarming(farmingContract).depositTo(farmingContractPool, _amount, msg.sender);
  }

  function _prepareWithdraw(uint256 _amount) private {
    require(vestedTokens[msg.sender].totalAmount > 0,"No tokens vested for this address");
    (, uint256 available) = withdrawableAmount(msg.sender);
    require(_amount <= available, "Amount exeeded current withdrawable amount");
    require(available > 0, "Nothing to withdraw");
    vestedTokens[msg.sender].withdrawnAmount += _amount;
    totalVestedAmount -= _amount;
    emit Withdrawal(msg.sender, _amount);
  } 
  
  /**
  * @param _beneficiary - address of the user who has his/her tokens vested on the contract
  * returns:
  * 0. next payout date for the user (0 if tokens are fully paid out)
  * 1. amount of tokens that user can withdraw as of now
  */
  function withdrawableAmount(address _beneficiary) public virtual view returns (uint64, uint256);

  /**
  * @param _beneficiary - address of the user who has his/her tokens vested on the contract
  * returns:
  * 0. payment plan ID
  * 1. vesting start date. no claims before start date allowed
  * 2. next unlock date. the date user can claim more tokens
  * 3. total amount of tokens vested
  * 4. withdrawn tokens amount (already claimed tokens, essentially) 
  * 5. amount of tokens that user can withdraw as of now
  */
  function vestingData(address _beneficiary) public view returns (uint8, uint64, uint64, uint256, uint256, uint256) {
    (uint64 nextUnlockDate, uint256 available) = withdrawableAmount(_beneficiary);
    return (vestedTokens[_beneficiary].paymentPlan, vestedTokens[_beneficiary].startDate, nextUnlockDate, vestedTokens[_beneficiary].totalAmount, vestedTokens[_beneficiary].withdrawnAmount, available);
  }

  function _isPaymentPlanExists(uint8 _id) internal virtual view returns (bool);

}
